#include <Adafruit_NeoPixel.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>

#define NUM_LEDS 48
#define PIN 11
#define DEBOUNCE_DELAY 50
#define LED_BRIGHTNESS 50

Adafruit_NeoPixel YagamyLight = Adafruit_NeoPixel(NUM_LEDS, PIN, NEO_RGB + NEO_KHZ800);
LiquidCrystal_I2C lcd1(0x27, 20, 4);

String desafioContinuacaoContinuacao;
String desafioContinuacao;
const int botoes[4] = {2, 5, 6, 7};
bool estadoAtual[4] = {LOW, LOW, LOW, LOW};
bool ultimoEstado[4] = {LOW, LOW, LOW, LOW};
unsigned long ultimoTempoDebounce[4] = {0, 0, 0, 0};
int posJogador[4] = {0, 0, 0, 0};
int vezJogador = 0;
bool esperandoProximoJogador = false;
bool jogoEncerrado = false;

bool casaTextura(int pos) {
  return (pos == 4 || pos == 11 || pos == 18 || pos == 25);
}

void printCentered(String msg, int row) {
  msg.trim();
  lcd1.setCursor(0, row);
  lcd1.print("                    ");
  int len = msg.length();
  int col = (20 - len) / 2;
  if (col < 0) col = 0;
  lcd1.setCursor(col, row);
  lcd1.print(msg);
}

void setup() {
  Serial.begin(9600);
  YagamyLight.begin();
  YagamyLight.setBrightness(LED_BRIGHTNESS);
  YagamyLight.show();

  for (int i = 0; i < 4; i++) {
    pinMode(botoes[i], INPUT_PULLUP);
  }

  lcd1.init();
  lcd1.backlight();
  randomSeed(analogRead(0));

  printCentered("Jogador 1 vez!", 0);
  printCentered("Aperte botao!", 1);
  mostrarLedJogador(vezJogador);
}

void mostrarLedJogador(int jogador) {
  YagamyLight.clear();

  // Define player colors (softer tones for sensory-friendly design)
  uint32_t cor;
  switch (jogador) {
    case 0: cor = YagamyLight.Color(200, 50, 50); break;   // Jogador 1: Soft red
    case 1: cor = YagamyLight.Color(50, 200, 50); break;   // Jogador 2: Soft green
    case 2: cor = YagamyLight.Color(50, 50, 200); break;   // Jogador 3: Soft blue
    case 3: cor = YagamyLight.Color(200, 200, 50); break;  // Jogador 4: Soft yellow
  }

  // Light all LEDs in the player's color during their turn
  for (int i = 0; i < 24; i++) {
    YagamyLight.setPixelColor(2 * i, cor);
    YagamyLight.setPixelColor(2 * i + 1, cor);
  }

  int casaAtual = posJogador[jogador];
  if (casaAtual > 0 && casaAtual <= 24) {
    int ledIndex1 = 2 * (casaAtual - 1);     // First LED of the pair
    int ledIndex2 = 2 * (casaAtual - 1) + 1; // Second LED of the pair
    YagamyLight.setPixelColor(ledIndex1, YagamyLight.Color(255, 255, 255));
    YagamyLight.setPixelColor(ledIndex2, YagamyLight.Color(255, 255, 255));
    if (casaTextura(casaAtual)) {
      for (int i = 0; i < 3; i++) {
        YagamyLight.setPixelColor(ledIndex1, cor);
        YagamyLight.setPixelColor(ledIndex2, cor);
        YagamyLight.show();
        delay(200);
        YagamyLight.setPixelColor(ledIndex1, YagamyLight.Color(255, 255, 255));
        YagamyLight.setPixelColor(ledIndex2, YagamyLight.Color(255, 255, 255));
        YagamyLight.show();
        delay(200);
      }
    }
  }
  YagamyLight.show();
}
void jogarDado(int jogador) {
  for (int i = 0; i < 10; i++) {
    int temp = random(1, 7);
    lcd1.clear();
    printCentered("Rolando: " + String(temp), 1);
    YagamyLight.clear();
    int randomLed = random(0, NUM_LEDS);
    uint32_t cor;
    switch (jogador) {
      case 0: cor = YagamyLight.Color(200, 50, 50); break;
      case 1: cor = YagamyLight.Color(50, 200, 50); break;
      case 2: cor = YagamyLight.Color(50, 50, 200); break;
      case 3: cor = YagamyLight.Color(200, 200, 50); break;
    }
    YagamyLight.setPixelColor(randomLed, cor);
    YagamyLight.show();
    delay(70);
    YagamyLight.clear();
    YagamyLight.show();
  }

  int numero = random(1, 7);
  Serial.println(numero);
  lcd1.clear();
  printCentered("Sorte: " + String(numero), 1);
  delay(800);

  posJogador[jogador] += numero;
  if (posJogador[jogador] > 28) {
    posJogador[jogador] = 28;
    lcd1.clear();
    printCentered(String("J") + String(jogador + 1) + " VENCEU!", 0);
    printCentered("J1: Resetar", 1);

    uint32_t cor;
    switch (jogador) {
      case 0: cor = YagamyLight.Color(200, 50, 50); break;
      case 1: cor = YagamyLight.Color(50, 200, 50); break;
      case 2: cor = YagamyLight.Color(50, 50, 200); break;
      case 3: cor = YagamyLight.Color(200, 200, 50); break;
    }
    for (int i = 0; i < 5; i++) {
      YagamyLight.fill(cor, 0, NUM_LEDS);
      YagamyLight.show();
      delay(300);
      YagamyLight.clear();
      YagamyLight.show();
      delay(300);
    }
    esperandoProximoJogador = false;
    jogoEncerrado = true;
    return;
  }

  lcd1.clear();
  printCentered(String("J") + String(jogador + 1) + " casa " + String(posJogador[jogador]), 0);

  if (casaTextura(posJogador[jogador])) {
    printCentered("Casa de TEXTURA!", 1);
  } else {
    int idx = posJogador[jogador] - 1;
    if (idx >= 0 && idx < 23) {
      if (Serial.available() > 0) {
        String desafio = Serial.readStringUntil('\n');
        if (desafio.length() > 20) {
          if(desafio.length() > 40) {
            desafioContinuacaoContinuacao = desafio.substring(40, 60);
            printCentered(desafioContinuacaoContinuacao, 3);
          }
          desafioContinuacao = desafio.substring(20, 40);
          desafio = desafio.substring(0, 20);
          printCentered(desafioContinuacao, 2);
        }
        printCentered(desafio, 1);

      }
    }
  }

  mostrarLedJogador(jogador);
}

void resetGame() {
  for (int i = 0; i < 4; i++) {
    posJogador[i] = 0;
  }
  vezJogador = 0;
  esperandoProximoJogador = false;
  jogoEncerrado = false;
  lcd1.clear();
  printCentered("Jogador 1 vez!", 0);
  printCentered("Aperte botao!", 1);
  YagamyLight.clear();
  YagamyLight.show();
  mostrarLedJogador(vezJogador);
}

void loop() {
  for (int i = 0; i < 4; i++) {
    bool leitura = digitalRead(botoes[i]);

    if (leitura != ultimoEstado[i]) {
      ultimoTempoDebounce[i] = millis();
    }

    if ((millis() - ultimoTempoDebounce[i]) > DEBOUNCE_DELAY) {
      if (leitura != estadoAtual[i]) {
        estadoAtual[i] = leitura;

        if (estadoAtual[i] == LOW) {
          if (jogoEncerrado && i == 0) {
            resetGame();
          } else if (!jogoEncerrado) {
            if (i == vezJogador && !esperandoProximoJogador) {
              jogarDado(i);
              esperandoProximoJogador = true;
            } else if (esperandoProximoJogador && i == (vezJogador + 1) % 4) {
              vezJogador = (vezJogador + 1) % 4;
              esperandoProximoJogador = false;
              lcd1.clear();
              printCentered(String("Jogador ") + String(vezJogador + 1) + " vez!", 0);
              printCentered("Aperte botao!", 1);
              mostrarLedJogador(vezJogador);
            }
          }
        }
      }
    }

    ultimoEstado[i] = leitura;
  }
}
